sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: DAO"
  }
}

dependencies {
  // please keep the list grouped by configuration and ordered by name

  compile 'com.google.guava:guava'
  compile 'com.google.protobuf:protobuf-java'
  compile 'commons-io:commons-io'
  compile 'commons-lang:commons-lang'
  compile 'net.jpountz.lz4:lz4'
  compile 'org.mybatis:mybatis'
  compile project(path: ':sonar-plugin-api', configuration: 'shadow')

  compile project(':server:sonar-db-core')
  compile project(':server:sonar-db-migration')
  compile project(':sonar-core')

  compileOnly 'com.google.code.findbugs:jsr305'

  testImplementation  'com.tngtech.java:junit-dataprovider'
  testImplementation  'commons-dbutils:commons-dbutils'
  testImplementation  'junit:junit'
  testImplementation  'org.assertj:assertj-core'
  testImplementation  'org.assertj:assertj-guava'
  testImplementation  'org.mockito:mockito-core'
  testImplementation  'org.sonarsource.orchestrator:sonar-orchestrator'
  testImplementation  project(':sonar-testing-harness')
  testImplementation  project(':sonar-plugin-api-impl')

  testCompileOnly 'com.google.code.findbugs:jsr305'

  testRuntimeOnly 'com.h2database:h2'
  testRuntimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
  testRuntimeOnly 'com.oracle.database.jdbc:ojdbc8'
  testRuntimeOnly 'org.postgresql:postgresql'

  testFixturesApi testFixtures(project(':server:sonar-db-core'))

  testFixturesImplementation 'com.h2database:h2'

  testFixturesCompileOnly 'com.google.code.findbugs:jsr305'
}

test {
  systemProperty 'orchestrator.configUrl', System.getProperty('orchestrator.configUrl')
}

task dumpSchema(type:JavaExec) {
    main = 'org.sonar.db.dump.DumpSQSchema'
    classpath = sourceSets.test.runtimeClasspath
}
tasks.check.dependsOn dumpSchema

task createDB(type:JavaExec) {
    main = 'org.sonar.db.createdb.CreateDb'
    classpath = sourceSets.test.runtimeClasspath
    systemProperty 'orchestrator.configUrl', System.getProperty('orchestrator.configUrl')
    if (!project.version.endsWith("-SNAPSHOT")) {
        systemProperty 'sonar.runtimeVersion', project.version
    }
}

task testJar(type: Jar) {
  classifier = 'tests'
  from sourceSets.test.output
}

configurations {
  tests
}

artifacts {
   tests testJar
}

jar {
    // remove exclusion on proto files so that they can be included by other modules
    setExcludes([])
}
