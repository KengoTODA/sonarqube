plugins {
  id 'java-library'
}

sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Scanner Engine"
  }
}

// Excluding scanner files in order to benefit of incremental build support
sourceSets.test.resources {
  exclude '**/.sonar/**'
}

configurations {
  testImplementation.extendsFrom(compileOnly)
}

dependencies {
  // please keep the list ordered

  api 'ch.qos.logback:logback-classic'
  api 'ch.qos.logback:logback-core'
  implementation 'commons-codec:commons-codec'
  api 'commons-io:commons-io'
  implementation 'commons-lang:commons-lang'
  implementation 'com.google.code.gson:gson'
  implementation 'org.apache.commons:commons-csv' // not used
  implementation 'com.google.protobuf:protobuf-java' // not used
  implementation 'com.squareup.okhttp3:okhttp'
  implementation 'com.fasterxml.staxmate:staxmate'
  api 'org.eclipse.jgit:org.eclipse.jgit'
  api 'org.tmatesoft.svnkit:svnkit'
  api 'org.picocontainer:picocontainer'
  runtimeOnly 'org.slf4j:jcl-over-slf4j'
  runtimeOnly 'org.slf4j:jul-to-slf4j'
  runtimeOnly 'org.slf4j:log4j-over-slf4j'
  implementation 'org.slf4j:slf4j-api'
  api 'org.sonarsource.update-center:sonar-update-center-common'

  api project(':sonar-core')
  api project(':sonar-scanner-protocol')
  api project(':sonar-ws')
  api project(':sonar-duplications')

  implementation project(':sonar-plugin-api-impl')
  api project(path: ':sonar-plugin-api', configuration: 'shadow')

  compileOnly 'com.google.code.findbugs:jsr305'

  testImplementation 'com.squareup.okhttp3:mockwebserver'
  testImplementation 'com.squareup.okhttp3:okhttp'
  testImplementation 'com.squareup.okio:okio'
  testImplementation 'com.tngtech.java:junit-dataprovider'
  testImplementation 'commons-io:commons-io'
  testImplementation 'junit:junit'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'com.fasterxml.staxmate:staxmate'
  testImplementation 'org.hamcrest:hamcrest-core'
  testImplementation 'org.mockito:mockito-core'
  testImplementation project(':plugins:sonar-xoo-plugin')
  testImplementation project(':sonar-plugin-api').sourceSets.test.output

}

license {
  excludes(["**/Fake.java", "**/Fake.groovy", "org/sonar/scanner/cpd/ManyStatements.java"])
}

artifactoryPublish.skip = false

// Used by core plugins
publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      if (release) {
        artifact sourcesJar
        artifact javadocJar
      }
    }
  }
}
